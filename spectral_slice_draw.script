#########################################################################
## This script draws a spectrogram and an LPC spectrum taken at a
## user-specified time point. The time point is marked on the 
## spectrogram. The input is a sound file; the script generates 
## all derived objects. The opening form asks the user at what
## time point the spectrum should be taken, how to label it in 
## the spectrogram, what to call the output files and where to save 
## them.
##
## Leendert Plug, November 2019
#########################################################################

form Settings
sentence Label cursor
positive Time 0.815
boolean Speckleformants 1
sentence Directory ...\
sentence Savespectrogram spectrogram
sentence Savespectrum spectrum
sentence Savesound sound
endform

## The next bit saves the sound file. This is useful if you have
## just recorded it. If using a stored sound file you can comment
## the second line.

object$ = selected$ ("Sound")
Write to WAV file... 'directory$'\'savesound$'.wav

## The next bit creates the spectrogram. 

To Spectrogram... 0.005 5000 0.002 20 Gaussian

## The next bit prepares the picture window and specifies basic
## drawing parameters.

Viewport... 0 5.5 0 3.5
14
Helvetica
Line width... 1
Erase all

## The next bit paints the spectrogram and saves it.

Paint... 0 0 0 0 100 no 50 4.5 0 yes
Marks left every... 1 1000 yes yes yes
One mark top... time no yes yes 'label$'

## The next bit adds the formant tracks

if speckleformants 
   select Sound 'object$'
   To Formant (burg)... 0 5 5000 0.025 50
   Speckle size... 1
   Magenta
   Speckle... 0 0 5000 30 no
   Black
endif

Save as 600-dpi PNG file... 'directory$'\'savespectrogram$'.png

## The next bit downsamples the sound file to allow for LPC 
## analysis (5 formants up to 5000 Hz, downsample to 10000). 
## LPC analysis is then done using prediction order 10.
## See the Praat manual. Other settings are the defaults.

select Sound 'object$'
Resample... 10000 50
To LPC (burg)... 10 0.025 0.005 50

## The next bit takes the spectral slice at the time point 
## specified in the opening form. Other settings are the defaults.

To Spectrum (slice)... time 20 0 50

## The next bit prepares the picture window to place the spectrum
## under the spectrogram.

Viewport... 0 4.5 3.5 6.5

## The next bit draws the spectral slice. If the peaks push the top 
## of the graph, you can increase the max amplitude by changing the 
## final zero to an appropriate value.

Draw... 0 0 0 0 yes
Marks bottom every... 1 1000 yes yes yes
Save as 600-dpi PNG file... 'directory$'\'savespectrum$'.png














