#######################################################################################
# This is a simple script to draw a waveform, spectrogram and pitch curve underneath.
# Start with the sound object selected; the rest is created. The settings for pitch
# object creation are the Praat defaults. The initial form asks only for the 
# directory in which the resulting picture should be saved and the pitch curve scale
# (maximum and intermediate intervals).
#
# Leendert Plug, April 2020
#######################################################################################

 form Settings
   sentence Directory C:\Users\lnplp\Desktop\Module materials\Phonetics 2019-20\lectures\lecture 9
   real maxf0 300
   real intf0 100
   endform

## The next bit creates the spectrogram and pitch object. 

   object$ = selected$ ("Sound")
   To Spectrogram... 0.005 5000 0.002 20 Gaussian
   select Sound 'object$'
   To Pitch... 0 75 600

## The next bit prepares the picture window and specifies basic
## drawing parameters.

   Erase all
   14
   Helvetica
 
## The next bit draws the waveform;

   select Sound 'object$'
   Viewport... 0 5.5 0 2
   Line width... 1
   Draw... 0 0 0 0 no
#   Draw inner box

## the spectrogram;

   select Spectrogram 'object$'
   Viewport... 0 5.5 1 3.5
   Paint... 0 0 0 0 100 no 50 4.5 0 no
   Marks left every... 1 1000 yes yes yes
   Draw inner box

## and the f0 trace. 

   Viewport... 0 5.5 2.6 4.3
   select Pitch 'object$'
   Line width... 4
   Draw... 0 0 0 maxf0 no
   Line width... 1
   Marks left every... 1 intf0 yes yes yes
   Draw inner box

## The next bit saves the whole picture.

   Viewport... 0 5.5 0 4.3
   Save as 600-dpi PNG file... 'directory$'\'object$'_pitch.png

## The next bit tidies up.

select all
minus Sound 'object$'
Remove
  
